begin parameters

     #Volumes
        NA 6.022e23 #mol^-1
        vol_um3 0.1205 #um3
        vol_CP vol_um3*1e-15 #volume L
        CHC1_0 100
        #Binding parameters (Kd in units of microMolar. Convert to molar, then get kon rate, then divide by NA)
        koff_chc 1
        c0 1 #1 M is the standard state conc
	KD 100 #uM

        #FOR SELF BINDING, NFSIM WILL DIVIDE BY 2, SO CORRECT WITH FACTOR OF 2 HERE.  kon=koff/KD
        kon_chc_chcM (2*koff_chc/(KD*1e-6)) #Liter/mol/second
        kon_chc_chc kon_chc_chcM/NA #Liter/second
#        kon_chc_chc_intra kon_chc_chc/vol_CP #kon_chc_chcM*c0 #1/second

         #Rate constants for cytoplasm
        kon_chc_chc_cy kon_chc_chc/vol_CP #1/second

end parameters

begin molecule types
        CHC1(a1,a2,a3)


end molecule types
begin seed species
        CHC1(a1,a2,a3) CHC1_0
end seed species

begin observables
        Molecules FreeCHC CHC1(a1,a2,a3)
        Molecules Clathrin CHC1()
        Molecules ClathrinDimer1 CHC1(a1!1).CHC1(a1!1)  #these get double counted, forward and backward!!
        Molecules ClathrinDimer2 CHC1(a2!1).CHC1(a2!1)
        Molecules ClathrinDimer3 CHC1(a3!1).CHC1(a3!1)
        Molecules HetDimer12 CHC1(a1!1).CHC1(a2!1)
        Molecules HetDimer13 CHC1(a1!1).CHC1(a3!1)
        Molecules HetDimer23 CHC1(a2!1).CHC1(a3!1)
	Molecules FreeA CHC1(a1)
        Molecules FreeB CHC1(a2)
        Molecules FreeC CHC1(a3)
	Molecules ClathrinDubA CHC1(a1!+,a2!+,a3)
        Molecules ClathrinDubB CHC1(a1!+,a2,a3!+)
	Molecules ClathrinDubC CHC1(a1,a2!+,a3!+)
        Molecules FullClathBound CHC1(a1!+,a2!+,a3!+)
end observables

begin functions
end functions

begin reaction rules
         # clathrin clathrin,
        CHC1(a1) + CHC1(a1) <-> CHC1(a1!1).CHC1(a1!1) kon_chc_chc_cy, koff_chc
	CHC1(a2) + CHC1(a2) <-> CHC1(a2!1).CHC1(a2!1) kon_chc_chc_cy, koff_chc
        CHC1(a3) + CHC1(a3) <-> CHC1(a3!1).CHC1(a3!1) kon_chc_chc_cy, koff_chc
        CHC1(a1) + CHC1(a2) <-> CHC1(a1!1).CHC1(a2!1) kon_chc_chc_cy, koff_chc
        CHC1(a2) + CHC1(a3) <-> CHC1(a2!1).CHC1(a3!1) kon_chc_chc_cy, koff_chc
        CHC1(a1) + CHC1(a3) <-> CHC1(a1!1).CHC1(a3!1) kon_chc_chc_cy, koff_chc

end reaction rules


simulate_nf({suffix=>"nf",t_end=>100,n_steps=>50000,param=>"-gml 2000000 -utl 10000000"});

#EOF

